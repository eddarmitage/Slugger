plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.10.1'
}

repositories {
    mavenCentral()
}

project.buildDir = 'target'

sourceSets {
    bdd {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.6.0')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core:3.15.0')
    bddImplementation('io.cucumber:cucumber-java:5.4.2')
    bddImplementation('io.cucumber:cucumber-picocontainer:5.4.2')
}

configurations {
    bddImplementation.extendsFrom testImplementation
    bddRuntimeOnly.extendsFrom runtimeOnly

    cucumberRuntime.extendsFrom bddImplementation
}

test {
    useJUnitPlatform()
}

task cucumber() {
    dependsOn assemble, compileBddJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.bdd.output
            args = ['--plugin', 'pretty', '--plugin', 'html:target/cucumber', '--glue', 'com.eddarmitage.slugger.bdd', 'src/bdd/resources']
        }
    }
}

check.dependsOn cucumber

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'target/reports/jacoco/test/jacocoTestReport.xml'
}
