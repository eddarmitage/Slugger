plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
}

scmVersion {
    ignoreUncommittedChanges = false
}

group = 'com.eddarmitage'
description = 'A Java library for creating nice URL Slugs'
version = scmVersion.version

repositories {
    mavenCentral()
}

sourceSets {
    cucumber {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    cucumberImplementation.extendsFrom testImplementation
    cucumberRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.5.2')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core:3.14.0')
    cucumberImplementation('io.cucumber:cucumber-java:4.8.0')
    cucumberRuntimeOnly('io.cucumber:cucumber-junit:4.8.0')
    cucumberRuntimeOnly('io.cucumber:cucumber-picocontainer:4.8.0')
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

task cucumber(type: Test) {
    testClassesDirs = sourceSets.cucumber.output.classesDirs
    classpath = sourceSets.cucumber.runtimeClasspath

    dependsOn classes
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntimeClasspath + sourceSets.main.output + sourceSets.cucumber.output
            args = ['--plugin', 'pretty', '--plugin', 'html:build/cucumber', '--glue', 'com.eddarmitage.slugger.bdd', 'src/cucumber/resources']
        }
    }
}

check.dependsOn cucumber

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

publishing {
    publications {
        slugger(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'testRepo'
            url = "file://${buildDir}/repo"
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/eddarmitage/Slugger")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
}
