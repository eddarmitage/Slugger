plugins {
    id 'java-library'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id 'com.palantir.git-version' version '0.13.0'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

apply from: file('gradle/cucumber.gradle')
apply from: file('gradle/test-reports.gradle')
apply from: file('gradle/publish.gradle')

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core:3.24.0')
    cucumberImplementation('io.cucumber:cucumber-java:7.10.1')
    cucumberImplementation('io.cucumber:cucumber-picocontainer:7.10.1')
    cucumberImplementation('org.assertj:assertj-core:3.24.0')
}

allprojects {
    archivesBaseName = 'slugger'
    group = 'com.eddarmitage'
    version = gitVersion()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

jar {
    manifest {
        attributes(
                'Implementation-Vendor': 'Edd Armitage',
                'Implementation-Title': 'Slugger',
                'Implementation-Version':  rootProject.version
        )
    }
}
