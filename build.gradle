plugins {
    id 'java'
    id 'jacoco'
    id "nebula.contacts" version "5.0.3"
    id "nebula.javadoc-jar" version "13.4.1"
    id "nebula.nebula-bintray-publishing" version "7.4.1"
    id "nebula.source-jar" version "13.1.0"
    id 'nebula.info' version '5.0.3'
    id 'nebula.integtest' version '6.0.3'
    id 'nebula.maven-publish' version '13.4.1'
    id 'nebula.release' version '11.1.2'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

group = 'com.eddarmitage'
description = 'A Java library for creating nice URL Slugs'

sourceCompatibility = 1.8
targetCompatibility = 1.8

contacts {
    'edward.armitage@gmail.com' {
        moniker 'Edd Armitage'
        github 'eddarmitage'
    }
}

repositories {
    mavenCentral()
}

project.buildDir = 'target'

sourceSets {
    bdd {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.5.2')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core:3.14.0')
    bddImplementation('io.cucumber:cucumber-java:4.8.0')
    bddImplementation('io.cucumber:cucumber-picocontainer:4.8.0')
}

configurations {
    bddImplementation.extendsFrom testImplementation
    bddRuntimeOnly.extendsFrom runtimeOnly

    cucumberRuntime.extendsFrom bddImplementation
}

test {
    useJUnitPlatform()
}

task cucumber() {
    dependsOn assemble, compileBddJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.bdd.output
            args = ['--plugin', 'pretty', '--plugin', 'html:target/cucumber', '--glue', 'com.eddarmitage.slugger.bdd', 'src/bdd/resources']
        }
    }
}

check.dependsOn cucumber

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}